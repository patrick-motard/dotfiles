# vim: set ft=zsh:
#region Notes

# What are these 'region' and 'endregion' sections?
# 'region' and 'endregion' are used throughout this file to
# provide folding in vscode, so that I can expand/collapse
# whole sections of this file for easier editing.

#endregion Notes

#region History
export HISTSIZE=100000 # max events for internal history
export SAVEHIST=100000 # max events in history file
export HISTFILE="${ZDOTDIR}/.zhistory"
# immediately append to history instead of on terminal close
setopt HIST_FIND_NO_DUPS
setopt EXTENDED_HISTORY         # record command start time
setopt INC_APPEND_HISTORY_TIME  # append command to history file immediately after execution
setopt SHARE_HISTORY
bindkey '^R' history-incremental-search-backward
alias history="fc -l 1"         # Ensure all history is shown
alias historyts="fc -f -l 1"    # Show all history, with timestamp
#endregion History

#region Completion
# Optimize compinit by using cache (only rebuild once per day)
autoload -Uz compinit
if [[ -n ${ZDOTDIR}/.zcompdump(#qN.mh+24) ]]; then
  compinit
else
  compinit -C
fi
#endregion Completion

#region SSH
eval "$(ssh-agent -s)" &> /dev/null
[[ -f ~/.ssh/github ]] && ssh-add -q ~/.ssh/github
[[ -f ~/.ssh/lahey ]] && ssh-add -q ~/.ssh/lahey
#endregion SSH

#region Linux specific
{{ if eq .chezmoi.os "linux" -}}
# rbenv setup instructions:
# https://developer.fedoraproject.org/start/sw/web-app/rails.html
export PATH="$HOME/.rbenv/bin:$PATH"
eval "$(rbenv init -)"
export PATH="$PATH:$HOME/.rbenv/plugins/ruby-build/bin"

# This ensures neovim can access 'neovim-ruby-host' to execute ruby commands from neovim.
# You can see if this is configured correctly by running :checkhealth
export PATH="$PATH:$(rbenv root)/versions/$(rbenv version-name)/bin"
{{ end -}}
#endregion Linux specific

# Add local bin directories to PATH (only if they exist)
[[ -d "$HOME/.local/bin" ]] && export PATH="$PATH:$HOME/.local/bin"
[[ -d "$HOME/.local/bin/ssh-create" ]] && export PATH="$PATH:$HOME/.local/bin/ssh-create"

#region OSX specific
{{ if eq .chezmoi.os "darwin" -}}

# Initialize rbenv
if command -v rbenv >/dev/null 2>&1; then
  eval "$(rbenv init - zsh)"
  # Ruby Neovim setup - ensures neovim can access 'neovim-ruby-host'
  # You can verify with :checkhealth in neovim
  export PATH="$PATH:$(rbenv root)/versions/$(rbenv version-name)/bin"
fi

# Tmux 256 color support
# https://gpanders.com/blog/the-definitive-guide-to-using-tmux-256color-on-macos/
export PATH="/opt/homebrew/opt/ncurses/bin:$PATH"
export TERMINFO_DIRS=$TERMINFO_DIRS:$HOME/.local/share/terminfo
export TERM=tmux-256color

{{end -}}
#endregion OSX specific

#region Zendesk-specific configuration
{{ if eq .chezmoi.hostname "GVXPDWWKWG" -}}

[[ -f ~/.ssh/zendesk-company ]] && ssh-add -qK ~/.ssh/zendesk-company

## BEGIN -- managed by dot-ansible:zendesk -- ##
# export PLUGINS=$plugins
[[ -f ~/.local/bin/zendesk_zshrc.sh ]] && source ~/.local/bin/zendesk_zshrc.sh
## END -- managed by dot-ansible:zendesk -- ##

# for the billing repo
export HUSKY=0

# CICD
source <(cicd completion zsh); compdef _cicd cicd

## BEGIN -- managed by workstation, role: aws_cli -- ##
alias awswhoami='aws sts get-caller-identity'
function awscreds {
    aws-work.sh $1
    export AWS_PROFILE=$1
}
## END -- managed by workstation, role: aws_cli -- ##

{{end -}}
#endregion Zendesk-specific configuration

#region Directories
setopt AUTO_CD           # Auto-change directory without typing cd.
setopt AUTO_PUSHD        # Push dirs to the stack when cd'ing.
setopt EXTENDED_GLOB     # Use extended globbing syntax.
setopt PUSHD_IGNORE_DUPS # Don't push duplicates to the stack.
setopt PUSHD_SILENT      # Do not print the directory stack after pushd or popd.

alias d='dirs -v'
for index ({1..9}) alias "$index"="cd +${index}"; unset index
#endregion Directories

#region Nodenv
# Lazy load nodenv to improve shell startup time
if [[ -d "$HOME/.nodenv" ]]; then
  export PATH="$HOME/.nodenv/bin:$PATH"

  # Lazy-load nodenv
  nodenv() {
    unset -f nodenv
    eval "$(command nodenv init - zsh)"
    export PATH="$(nodenv root)/versions/$(nodenv version-name)/bin:$PATH"
    nodenv "$@"
  }
fi
#endregion Nodenv

# Load aliases at the end to ensure they aren't clobbered.
source $ZDOTDIR/aliases.zsh

#region Zplug
# zplug is a plugin manager for zsh. I install it initially
# using ansible. Specifically ansible/roles/zsh
source ~/.zplug/init.zsh
# vi mode
zplug "plugins/vi-mode", from:oh-my-zsh
# terraform plugin (provides tf_prompt_info for agnoster theme)
zplug "plugins/terraform", from:oh-my-zsh

# these two lines remove the user from prompt
DEFAULT_USER=$USER
prompt_context() {}
zplug "themes/agnoster", from:oh-my-zsh

# zplug "plugins/git", from:oh-my-zsh
zplug "zsh-users/zsh-autosuggestions"
# Don't use the zsh-vi-mode plugin. It breaks the keybindings
# in zsh-autosuggestions like using 'lk' for "accept".
# export ZVM_VI_ESCAPE_BINDKEY="fd"
# zplug "jeffreytse/zsh-vi-mode"
# syntax highlighting must be the last plugin sourced
zplug "zsh-users/zsh-syntax-highlighting", defer:2

zplug load
#endregion Zplug

#region zoxide
# Fast zsh directory switching (lazy-loaded)
if command -v zoxide >/dev/null 2>&1; then
  # Lazy-load zoxide
  z() {
    unset -f z
    eval "$(zoxide init zsh)"
    z "$@"
  }

  zi() {
    unset -f zi
    eval "$(zoxide init zsh)"
    zi "$@"
  }
fi
#endregion zoxide

#region fzf
if command -v fzf >/dev/null 2>&1; then
    source <(fzf --zsh)
else
    echo "fzf is not installed. Please install it to use its features."
fi
#endregion fzf

#region Custom Key Bindings

# ## Colemak-DH layout
# # rebind esc for vim mode
# bindkey -M viins 'ts' vi-cmd-mode
# # this is for the zsh-autosuggetions plugin
# bindkey 'nn' autosuggest-accept
# bindkey 'nc' autosuggest-clear
# # bindkey 'ln' autosuggest-toggle

## Qwerty layout.
# rebind esc for vim mode
bindkey -M viins 'fd' vi-cmd-mode
# this is for the zsh-autosuggetions plugin
bindkey 'lk' autosuggest-accept
bindkey 'lc' autosuggest-clear
# bindkey 'ln' autosuggest-toggle

# Sesh - tmux session manager.
# This function allows you to select a tmux session from a list and connect to it
# when not already in a tmux session.
# https://github.com/joshmedeski/sesh
function sesh-sessions() {
  {
    exec </dev/tty
    exec <&1
    local session
    session=$(sesh list -t -c | fzf --height 40% --reverse --border-label ' sesh ' --border --prompt 'âš¡  ')
    zle reset-prompt > /dev/null 2>&1 || true
    [[ -z "$session" ]] && return
    sesh connect $session
  }
}
zle     -N             sesh-sessions
bindkey -M vicmd '\es' sesh-sessions
bindkey -M viins '\es' sesh-sessions
#endregion Custom Key Bindings


